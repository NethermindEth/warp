// SPDX-License-Identifier: LGPL-3.0-only
pragma solidity ^0.8.0;
import '../common/SelfAuthorized.sol';

/// @title OwnerManager - Manages a set of owners and a threshold to perform actions.
/// @author Stefan George - <stefan@gnosis.pm>
/// @author Richard Meissner - <richard@gnosis.pm>
contract OwnerManager is SelfAuthorized {
  address internal constant SENTINEL_OWNERS = address(0x1);

  mapping(address => address) internal owners;
  uint256 internal ownerCount;
  uint256 internal threshold;

  /// @dev Setup function sets initial storage of contract.
  /// @param _owners List of Safe owners.
  /// @param _threshold Number of required confirmations for a Safe transaction.
  function setupOwners(address[] memory _owners, uint256 _threshold) internal {
    // Threshold can only be 0 at initialization.
    // Check ensures that setup function can only be called once.
    require(threshold == 0, 'GS200');
    // Validate that threshold is smaller than number of added owners.
    require(_threshold <= _owners.length, 'GS201');
    // There has to be at least one Safe owner.
    require(_threshold >= 1, 'GS202');
    // Initializing Safe owners.
    address currentOwner = SENTINEL_OWNERS;
    for (uint256 i = 0; i < _owners.length; i++) {
      // Owner address cannot be null.
      address owner = _owners[i];
      require(
        owner != address(0) &&
          owner != SENTINEL_OWNERS &&
          owner != address(this) &&
          currentOwner != owner,
        'GS203'
      );
      // No duplicate owners allowed.
      require(owners[owner] == address(0), 'GS204');
      owners[currentOwner] = owner;
      currentOwner = owner;
    }
    owners[currentOwner] = SENTINEL_OWNERS;
    ownerCount = _owners.length;
    threshold = _threshold;
  }

  /// @dev Allows to add a new owner to the Safe and update the threshold at the same time.
  ///      This can only be done via a Safe transaction.
  /// @notice Adds the owner `owner` to the Safe and updates the threshold to `_threshold`.
  /// @param owner New owner address.
  /// @param _threshold New threshold.
  function addOwnerWithThreshold(address owner, uint256 _threshold) public authorized {
    // Owner address cannot be null, the sentinel or the Safe itself.
    require(
      owner != address(0) && owner != SENTINEL_OWNERS && owner != address(this),
      'GS203'
    );
    // No duplicate owners allowed.
    require(owners[owner] == address(0), 'GS204');
    owners[owner] = owners[SENTINEL_OWNERS];
    owners[SENTINEL_OWNERS] = owner;
    ownerCount++;
    // Change threshold if threshold was changed.
    if (threshold != _threshold) changeThreshold(_threshold);
  }

  /// @dev Allows to remove an owner from the Safe and update the threshold at the same time.
  ///      This can only be done via a Safe transaction.
  /// @notice Removes the owner `owner` from the Safe and updates the threshold to `_threshold`.
  /// @param prevOwner Owner that pointed to the owner to be removed in the linked list
  /// @param owner Owner address to be removed.
  /// @param _threshold New threshold.
  function removeOwner(
    address prevOwner,
    address owner,
    uint256 _threshold
  ) public authorized {
    // Only allow to remove an owner, if threshold can still be reached.
    require(ownerCount - 1 >= _threshold, 'GS201');
    // Validate owner address and check that it corresponds to owner index.
    require(owner != address(0) && owner != SENTINEL_OWNERS, 'GS203');
    require(owners[prevOwner] == owner, 'GS205');
    owners[prevOwner] = owners[owner];
    owners[owner] = address(0);
    ownerCount--;
    // Change threshold if threshold was changed.
    if (threshold != _threshold) changeThreshold(_threshold);
  }

  /// @dev Allows to swap/replace an owner from the Safe with another address.
  ///      This can only be done via a Safe transaction.
  /// @notice Replaces the owner `oldOwner` in the Safe with `newOwner`.
  /// @param prevOwner Owner that pointed to the owner to be replaced in the linked list
  /// @param oldOwner Owner address to be replaced.
  /// @param newOwner New owner address.
  function swapOwner(
    address prevOwner,
    address oldOwner,
    address newOwner
  ) public authorized {
    // Owner address cannot be null, the sentinel or the Safe itself.
    require(
      newOwner != address(0) && newOwner != SENTINEL_OWNERS && newOwner != address(this),
      'GS203'
    );
    // No duplicate owners allowed.
    require(owners[newOwner] == address(0), 'GS204');
    // Validate oldOwner address and check that it corresponds to owner index.
    require(oldOwner != address(0) && oldOwner != SENTINEL_OWNERS, 'GS203');
    require(owners[prevOwner] == oldOwner, 'GS205');
    owners[newOwner] = owners[oldOwner];
    owners[prevOwner] = newOwner;
    owners[oldOwner] = address(0);
  }

  /// @dev Allows to update the number of required confirmations by Safe owners.
  ///      This can only be done via a Safe transaction.
  /// @notice Changes the threshold of the Safe to `_threshold`.
  /// @param _threshold New threshold.
  function changeThreshold(uint256 _threshold) public authorized {
    // Validate that threshold is smaller than number of owners.
    require(_threshold <= ownerCount, 'GS201');
    // There has to be at least one Safe owner.
    require(_threshold >= 1, 'GS202');
    threshold = _threshold;
  }

  function getThreshold() public view returns (uint256) {
    return threshold;
  }

  function isOwner(address owner) public view returns (bool) {
    return owner != SENTINEL_OWNERS && owners[owner] != address(0);
  }

  /// @dev Returns array of owners.
  /// @return Array of Safe owners.
  function getOwners() public view returns (address[] memory) {
    address[] memory array = new address[](ownerCount);

    // populate return array
    uint256 index = 0;
    address currentOwner = owners[SENTINEL_OWNERS];
    while (currentOwner != SENTINEL_OWNERS) {
      array[index] = currentOwner;
      currentOwner = owners[currentOwner];
      index++;
    }
    return array;
  }
}
